plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.1'
  id 'pl.allegro.tech.build.axion-release' version '1.2.3'
  id 'maven-publish'
}

scmVersion {
  tag { prefix = 'v' }
}

group = 'uk.org.cse'
version = scmVersion.version
sourceCompatibility = 1.7

ext.lucene = "4.0.0"

mainClassName = 'uk.org.cse.boilermatcher.main.BoilerMatcher'

shadowJar {
  baseName = 'boilermatcher-all'
  classifier = ''
}

publishing {
  publications {
    shadow(MavenPublication) {
      from components.shadow
      artifactId = 'boilermatcher-all'
    }
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url "http://repo.research.cse.org.uk/artifactory/libs-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}-local"
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  testCompile 	'junit:junit:4.12'
  compile 		'cglib:cglib-nodep:2.2.2'
  compile 		'org.parboiled:parboiled-java:1.1.4'
  compile		'com.google.guava:guava:18.0'
  compile		'joda-time:joda-time:2.1'
  compile 		'org.pojomatic:pojomatic:2.0.1'
  compile 		'commons-io:commons-io:2.4'
  
  compile    "org.apache.lucene:lucene-core:${lucene}"
  compile    "org.apache.lucene:lucene-analyzers-common:${lucene}"
  compile    "org.apache.lucene:lucene-sandbox:${lucene}"
  compile    "org.apache.lucene:lucene-suggest:${lucene}"
}

task wrapper(type: Wrapper) {
     gradleVersion = '2.4'
}